buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
version = '0.1'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'io.vertx:vertx-core:3.3.0-SNAPSHOT'
    compile 'io.vertx:vertx-lang-js:3.3.0-SNAPSHOT'
    compile 'io.vertx:vertx-mongo-client:3.3.0-SNAPSHOT'
    compile 'io.vertx:vertx-auth-mongo:3.3.0-SNAPSHOT'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'js:main.js'

    }

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

configurations {
  provided
  testCompile.extendsFrom provided
}

// Map the 'provided' dependency configuration to the appropriate IDEA visibility scopes.
plugins.withType(IdeaPlugin) {
  idea {
    module {
      scopes.PROVIDED.plus += [configurations.provided]
      scopes.COMPILE.minus += [configurations.provided]
      scopes.TEST.minus += [configurations.provided]
      scopes.RUNTIME.minus += [configurations.provided]
    }
  }
}
